grammar PGN    

# PGN Spec: http://www.saremba.de/chessgml/standards/pgn/pgn-complete.htm
# Representation of a full game in pgn format, tag_pairs, movetext and score
game <- tag_pairs:tag_pairs [\n]? movetext:movetext score:score? %make_game

# A tag pair is a key, value store in the form [Key "Value"]
tag_pairs <- tag_pair* %make_tag_pairs
tag_pair <- "[" dlm key dlm "\"" value "\"" dlm "]" dlm %make_tag_pair
key <- [A-Za-z0-9_]+
value <- [^\"]*

# Movetext is one or more moves, terminated by a score, like 1. "e4 e5 0-1"
movetext <- move* %make_movetext
move <- move_number dlm white:san dlm wcomment:comment? dlm black:san dlm bcomment:comment? dlm
move_number <- [0-9]+ "." 
# A SAN is a standard algebraic notation representation of a single players move, like e4
san <- (square / san_psq / file takes square / castle) promotes? check?
san_psq <- piece square takes? square / piece takes? square / piece file takes? square / piece rank takes? square 
piece <- [KQRNBP] 
disam <- square / file / rank 
takes <- "x"
square <- file rank
file <- [a-h]
rank <- [1-8]
promotes <- "=" [KQRNB]
check <- [+#]
castle <- "O-O-O" / "O-O"
comment <- "{" [^\}]* "}" %make_comment
# The score is one of the following, * being unknown
score <- "1-0" / "0-1" / "1/2-1/2" / "*"

# A possible whitespace delimeter
dlm <- [\s]?
